## Cairo

### The basics

The display of the image is handled by Cairo, a C library for two-dimensional drawing. Julia's Cairo wrapper isn't currently documented, so let's walk through a couple of basics first.

If you're new to graphics libraries like Cairo, there are a few concepts that may not be immediately obvious but are introduced in the Cairo [tutorial](http://cairographics.org/tutorial/). The key concept is that the Cairo API works like "stamping," where a _source_ gets applied to a _destination_ in a region specified by a _path_. Here, the destination will be the pixels corresponding to a region of a window on the screen. We'll control the source and the path to achieve the effects we want.

Let's play with this. First, inside a new window we create a Cairo Canvas for drawing:
```
using Base.Graphics
using Cairo
using Tk

win = Toplevel("Test", 400, 200)
c = Canvas(win)
pack(c, {:expand => true, :fill => "both"})
```
We've created a window 400 pixels wide and 200 pixels high. `c` is our Canvas, a type defined in the `Tk` package. Later we'll dig into the internals a bit, but for now suffice it to say that a Canvas is a multi-component object that you can often treat as a black box. The initial call creating the canvas leaves a lot of its fields undefined, because you don't yet know crucial details like the size of the canvas. The call to `pack` specifies that this canvas fills the entire window, and simultaneously fills in the missing information. Note currently that the window is blank, because we haven't drawn anything to it yet, and you can see anything lying underneath.

Cairo doesn't know anything about Tk Canvases, so we have to pull out the part of it that works directly with  Cairo:
```
ctx = getgc(c)
```
`getgc` means "get graphics context," returning an object (here `ctx`) that holds all relevant information about the current state of drawing in Cairo.

One nice feature of Cairo is that the coordinates are abstracted; ultimately we care about screen pixels, but we can set up _user coordinates_ that have whatever scaling is natural to the problem. We just have to tell Cairo how to convert user coordinates to _device_ (screen) coordinates. (It's also fair to say that the process of coordinate conversion can be a source of confusion.) We set up a coordinate system using `set_coords`, defined in `base/graphics.jl`:
```
function set_coords(ctx::GraphicsContext, x, y, w, h, l, r, t, b)
```
`x` (horizontal) and `y` (vertical) specify the upper-left corner of the drawing region in _device_ coordinates, and `w` and `h` its width and height, respectively. (Note Cairo uses (0,0) for the top-left corner of the window.) `l`, `r`, `t`, and `b` are the _user_ coordinates corresponding to the left, right, top, and bottom, respectively, of this region. Note that `set_coords` will also `clip` any drawing that occurs outside the region defined by `x`, `y`, `w`, and `h`; however, the coordinate system you've specified extends to infinity, and you can draw all the way to the edge of the canvas by calling `reset_clip()`.

Let's fill the drawing region with a color, so we can see it:
```
# Set coordinates to go from 0 to 10 within a 300x100 centered region
set_coords(ctx, 50, 50, 300, 100, 0, 10, 0, 10)
set_source_rgb(ctx, 0, 0, 1)   # set color to blue
paint(ctx)                     # paint the entire clip region
```
Perhaps surprisingly, nothing happened. The reason is that the Tk Canvas implements a technique called [double buffering](http://en.wikipedia.org/wiki/Multiple_buffering#Double_buffering_in_computer_graphics), which means that you do all your drawing to a hidden surface, and then blit the completed result to the screen. We can see this in action simply by bringing another window over the top of the window we're using to draw, and then bringing our window back to the top; suddenly you'll see a nice blue rectangle within the window, surrounded by whatever is in the background window(s). Fortunately, to display your graphics you don't have to rely on users changing the stacking order of windows; simply call `reveal(c)` any time you want to update the screen with the contents of the back (hidden) surface.

Now let's draw a red line:
```
move_to(ctx, -1, 5)
line_to(ctx, 7, 6)
set_source_rgb(ctx, 1, 0, 0)
set_line_width(ctx, 5)
stroke(ctx)
reveal(c)
```
We started at a position outside the coordinate region (we'll get to see the clipping in action this way). The next command, `line_to`, creates a segment of a _path_, the way that regions are defined in Cairo. The `stroke` command draws a line, after which the path is cleared. (You can use `stroke_preserve` if you want to re-use this path for another purpose later.)

Let's illustrate this by adding a solid green rectangle with a magenta border, letting it spill over the edges of the previously-defined coordinate region:
```
reset_clip(ctx)
rectangle(ctx, 7, 5, 4, 4)
set_source_rgb(ctx, 0, 1, 0)
fill_preserve(ctx)
set_source_rgb(ctx, 1, 0, 1)
stroke(ctx)
reveal(c)
```
`fill` differs from `paint` in that `fill` works inside the currently-defined path, whereas `paint` fills the entire clip region.

Here is our masterpiece, where the "background" may differ for you (mine was positioned over the bottom of a wikipedia page):
![cairo snapshot](cairo_example.jpg)

### Rendering an image

Images are rendered in Cairo by `fill` inside a path defined by `rectangle`. So far this is just like the simple drawing above. The difference is the _source_, which now will be a _surface_ instead of an RGB color. If you're drawing from Julia, chances are that you want to display an in-memory array. The main trick is that Cairo requires a matrix of type `Uint32` to encode the color, where the least significant byte is the blue value (ranging from `0x00` to `0xff`), the next is green, and the next red. (The most significant byte can encode the alpha, or transparency, value if you specify that transparency is to be used in your surface.)

Both `Winston` and `Images` can generate a buffer of `Uint32` for you. Let's try the one in `Images`:
```
using Images
img = imread("some_photo.jpg")
buf = uint32color(img)
image(ctx, CairoRGBSurface(buf'), 0, 0, 10, 10)
reveal(c)
```
You'll note the image is squashed, but should otherwise be displayed within your window. (Note `CairoRGBSurface` by default takes a transpose for you, because of the column-major order of matrices in Julia and the row-major convention of Cairo. `Images` avoids taking transposes unless necessary, so here we do a transpose that will later be undone by `CairoRGBSurface`. You can avoid the default behavior of `CairoRGBSurface` by calling `CairoImageSurface` directly, see the `Cairo.jl` code.)

### Resize support

A basic feature of windows is that they should behave properly under resize. This doesn't come entirely for free, although the grid (and pack) managers of Tk take care of many details for us. However, for Canvases we need to provide explicit support; to see what I mean, just try resizing the window we created above.

The key is to have a callback activated whenever the canvas changes size. Arranging this is very simple:
```
tk_bind(c.c, "<Configure>", path -> resize(arg1, ...))
```
Here again `c` is our Canvas, and `c.c` refers to a `TkWidget` within the Canvas object (something that Tk knows how to handle). We write the `resize` function ourselves, supplying whatever arguments we need via the anonymous function. Inside the `resize` function there need to be a couple of key ingredients, illustrated in the following example:
```
function resize(c::Canvas, buf)
    Tk.configure(c)
    ctx = getgc(c)
    set_coords(ctx, 50, 50, Tk.width(c)-100, Tk.height(c)-100, 0, 10, 0, 10)
    image(ctx, CairoRGBSurface(buf), 0, 0, 10, 10)
    reveal(c)
end
tk_bind(c.c, "<Configure>", path -> resize(c, buf))
```
The call to `Tk.configure` destroys existing Cairo contexts and creates new ones. One consequence is that your `ctx` variable *is now invalid, and using it will cause a segfault*. For any new drawing, you should grab a new `ctx` by calling `getgc(c)` again. We have to re-create our coordinate system, because that too was destroyed. Finally, we redraw the image. Note we didn't have to go through the process of converting to `Uint32`-based color again, it's only the interaction with Cairo that needs to be redone.

### Canvases and the mouse

A Canvas already comes with a set of fields prepared for mouse events. For example, in our GUI we have the equivalent of the following:
```
donefunc = (c, bb) -> zoombb(imgc, img2, bb)
c.mouse.button1press = (c, x, y) -> rubberband_start(c, x, y, donefunc)
```
`donefunc` is a callback to be executed when the user releases the mouse button. `c` is our canvas, `bb` is a `BoundingBox` (defined in `base/graphics.jl`) that here specifies the region selected by the user. (The first two inputs, `imgc` and `img2`, store settings that are relevant to this particular GUI but won't be described in detail here.) The second line binds a click of the left mouse button to the function `rubberband_start`, defined in `rubberband.jl`.

The `mouse` `MouseHandler` inside a `Canvas` has fields for `press` and `release` of all 3 mouse buttons, plus ones for motion. A few cases, relevant to this GUI, are not present in this type. These therefore require a bit of extra handling, for example:
```
# Bind double-clicks
tk_bind(c.c, "<Double-Button-1>", (path,x,y)->zoom_reset(imgc, img2))
# Bind the wheel mouse
bindwheel(c.c, "Shift", (path,delta)->panhorz(imgc,img2,int(delta)))
```

### The rubber band (region selection)

Support for the rubber band is provided in the file `src/rubberband.jl`. Like `navigation.jl`, this is a stand-alone set of functions that you should be able to incorporate into other projects. It draws a dashed rectangle that employs the same machinery we described at the top of this page, with slight modifications to create the dashes (through the `set_dash` function). This should all be fairly straightforward.

However, these functions use one additional trick worth mentioning. Let's finally look at the Tk `Canvas` object:
```
type Canvas
    c::TkWidget
    front::CairoSurface  # surface for window
    back::CairoSurface   # backing store
    frontcc::CairoContext
    backcc::CairoContext
    mouse::MouseHandler
    redraw
    
    function ...
```
Here we can see the two buffers, used in double-buffering, explicitly. `getgc(c)` simply returns `backcc`, so all drawing occurs on the back surface. For the rubber band, we draw explicitly to the front context, and then (as the size of the rubber band changes) "repair the damage" by copying from the back surface. Since we only have to modify the pixels along the band itself, this is fast.
    
### Conclusions

All the rest are basically details specific for this particular GUI. A fair amount of code is needed to handle coordinates: selecting specific regions within the image (in image pixels), vs. regions of the output canvas. This needs to be flexible, for example handling aspect ratio constraints when they are present. If you want to dive into these details, your best bet is to start reading through the code, but it's not going to be covered in any more detail here.

Hopefully by this point you have a pretty good sense for how to produce on-screen output with Tk and Cairo. The combination takes a little getting used to, but the end result is quite powerful. Happy hacking!
